plugins {
	id 'org.springframework.boot' version '2.7.2'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'java'
}

group = 'com.api'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2020.0.2")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

task buildZip(type: Zip) {
	from compileJava
	from processResources
	into('lib') {
		from configurations.runtimeClasspath
	}
}

dependencies {
	implementation 'org.springframework.cloud:spring-cloud-starter-function-web'
	// [AWS] Spring Cloud Function 을 위한 의존성
	implementation 'org.springframework.cloud:spring-cloud-function-adapter-aws'
	// [AWS] Lambda 에 배포하기 위한 의존성
	implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.1'
	// [AWS] Lambda 이벤트를 다루기 위한 의존성
	implementation group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '3.9.0'
	//API Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	// [DB] JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// [DB] MyBatis
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0'
	// [DB] JDBC
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client'
	// [Utillity] JSoup
	implementation 'org.jsoup:jsoup:1.14.2'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
	// [Utillity] Lombok
	compileOnly (group: 'org.projectlombok', name: 'lombok', version: '1.18.24')
	annotationProcessor(group: 'org.projectlombok', name: 'lombok', version: '1.18.24')
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

def reactAppDir = "$projectDir/src/main/webapp/reactapp"

sourceSets {
	main {
		resources {
			srcDirs = ["$projectDir/src/main/resources"]
		}
	}
}

processResources {
	dependsOn "copyReactFile"
}

task installReact(type: Exec) {
	workingDir "$reactAppDir"
	inputs.dir "$reactAppDir"
	group = BasePlugin.BUILD_GROUP
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine "npm.cmd", "audit", "fix"
		commandLine 'npm.cmd', 'install'
	} else {
		commandLine "npm", "audit", "fix"
		commandLine 'npm', 'install'
	}
}

task buildReact(type: Exec) {
	dependsOn "installReact"
	workingDir "$reactAppDir"
	inputs.dir "$reactAppDir"
	group = BasePlugin.BUILD_GROUP
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine "npm.cmd", "run-script", "build"
	} else {
		commandLine "npm", "run-script", "build"
	}
}

task copyReactFile(type: Copy) {
	dependsOn "buildReact"
	from "$reactAppDir/build"
	into "$buildDir/resources/main/static"
}